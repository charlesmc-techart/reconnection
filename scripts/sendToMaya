#!/bin/zsh

################################################################################
# Send a Python script to Maya via a socket opened by `commandPort`
#
# The script must be under src/rec/, and the socket must be /tmp/vscodeToMaya
################################################################################

source "${0:h}/errorMessage.zsh"

readonly SOCKET='/tmp/vscodeToMaya'

if [[ ! -f "$1" ]]; then
  errorMessage "${0:t}: ${1:P}: Not a file"
elif [[ "${1:e}" != 'py' ]]; then
  errorMessage "${0:t}: ${1:P}: Not a Python file"
fi;

readonly parentDir="${1:h:t}"
if [[ "${parentDir}" != 'rec' ]]; then
  errorMessage "${0:t}: Script must be from src/rec/"
fi;

readonly origScript="$(cat << EOF
from ${parentDir}.${1:t:r} import main

main()
EOF
)"

trap 'rm "${tempScript}"' EXIT INT
readonly tempScript="$(mktemp -t 'rec')"

echo "${origScript}" > "${tempScript}"
nc -U "/tmp/vscodeToMaya" < "${tempScript}"
if (( "$?" > 0 )); then
  errorMessage "${0:t}: Socket ${SOCKET} is not open"
fi